plugins {
    id 'org.jetbrains.intellij' version '0.5.0' apply false
}

group = 'de.lukweb.share'
version '1.0-SNAPSHOT'

// The default configuration for every submodule
subprojects {

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.jetbrains.intellij'
    group = 'de.lukweb.share'
    
    repositories {
        mavenCentral()
    }
    
    sourceCompatibility = 1.8
    
    dependencies {
        testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.0")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.7.0")
    }

    intellij {
        // https://www.jetbrains.org/intellij/sdk/docs/reference_guide/intellij_artifacts.html
        // Full list of IntelliJ IDEA releases: https://www.jetbrains.com/intellij-repository/releases
        // Full list of IntelliJ IDEA snapshots: https://www.jetbrains.com/intellij-repository/snapshots/
        version '203-EAP-SNAPSHOT'
        // Disables updating until-build attribute in plugin.xml
        updateSinceUntilBuild false
    }
    
    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
    
}

// All of the default configuration for the Intellij plugins
configure(subprojects.findAll { it.name == 'HasteIt' || it.name == 'DiscordBeam'}) {

    publishPlugin {
        // Publishing with gradle.properties
        username project.ext.properties.publishUsername
        token project.ext.properties.publishToken
    }
    
}
